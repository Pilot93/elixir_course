# Словари (Map)

Поскольку модуль называется Map, было бы правильнее перевести его как "отображение". Имеется в виду отображение (mapping) ключей на значения. Но это довольно редкий термин. В большинстве языков для такой структуры данных используется термин "словарь" (dictionary), поэтому я буду использовать его.

Итак, Map - это key-value структура данных, аналогичная словарю в Python или HashMap в Java.

## Cоздание словарей и чтение по ключу

Есть два синтаксиса для работы со словарями.

Основной синтаксис:

```elixir
m = %{"a" => 42, "b" => 100}
m["a"]
m["b"]
```

И синтаксический сахар для частного случая, где ключи являются атомами:

```elixir
m = %{a: 42, b: 100}
m.a
m.b
```

Такой синтаксический сахар очень удобный, и он поощряет использовать ключи-атомы. Но вообще ключом может быть любое значение, так как все значения в Эликсир иммутабельны.

Однако, между двумя способами работы со словарями разница есть не только в синтаксисе. Ещё отличается поведение при доступе к несуществующему ключу:

```elixir-iex
iex(5)> m.c
** (KeyError) key :c not found in: %{a: 42, b: 100}

iex(5)> m[:c]
nil
```

Это вопрос намерений. Если мы считаем, что отсутствие ключа в словаре является нештатной ситуацией, то стоит использовать чтение значения через точку. И тогда мы будем получать исключение, что и должно происходить в нештатной ситуации.

А если мы считаем отсутствие ключа нормальной, штатной ситуацией, то стоит использовать чтение через квадратные скобки даже для ключей-атомов. Тогда мы будем получать `nil`, что означает отсутствие значения.

## Чтение значений функциями get и fetch

Есть ещё один способ чтения значений -- функции get и fetch модуля Map. 
### Map.get

```elixir-iex
iex(11)> Map.get(m, :a)
42
iex(12)> Map.get(m, :d)
nil
iex(13)> Map.get(m, :d, 200)
200
```

Это удобно, когда нужно передать дефолтное значение для отсутствующего ключа. 

### Map.fetch

```elixir
> Map.fetch(m, :a)
{:ok, 42}
> Map.fetch(m, :d)
:error
```

Это каноничный функциональный подход. Ожидается использование либо с ветвлением:

```elixir
case Map.fetch(m, :a) do
  {:ok, value} -> IO.puts(value)
  :error -> IO.puts("not found")
end
```

либо, если отсутствие ключа в map является нештатной ситуаций, то ожидаем crash -- аварийную остановку процесса:

```elixir
> {:ok, value} = Map.fetch(m, :a)
{:ok, 42}
> {:ok, value} = Map.fetch(m, :d)
** (MatchError) no match of right hand side value: :error
```

### Map.fetch!

```elixir
> value = Map.fetch!(m, :a)
42
> value = Map.fetch!(m, :d)
** (KeyError) key :d not found in: %{a: 42, ...
```

Это не типично для функционального языка. Опытный ФП разработчик предпочтёт первый вариант:

```elixir
{:ok, value} = Map.fetch(m, :d)
```

И это даст такой же эффект, как:

```elixir
value = Map.fetch!(m, :d)
```

Ну, почти такой же. Исключения будут разные. И, может быть, KeyError тут более уместно, чем MatchError.

Есть ещё чтение значений с помощью сопоставления с образцом, но эту тему мы рассмотрим позже.

## Добавление ключей

### Map.put

```elixir
put(map(), key(), value()) :: map()
```

```elixir-iex
iex(15)> room = event.location.room
%{floor: 6, number: 610}
iex(16)> Map.put(room, :floor, 7)
%{floor: 7, number: 610}
iex(17)> Map.put(room, :size, :big)
%{floor: 6, number: 610, size: :big}
```

Функция обновляет существующий ключ или добавляет новый.

### Map.put_new

```elixir
put_new(map(), key(), value()) :: map()
```

Если обновление существующего ключа -- нежелательный эффект, т.к. мы не хотим по-ошибке затереть его, сделав опечатку, то лучше воспользоваться функцией put_new.

```elixir-iex
iex(19)> Map.put_new(room, :size, :big)
%{floor: 6, number: 610, size: :big}
iex(20)> Map.put_new(room, :floor, 7)
%{floor: 6, number: 610}
```

Поведение с существующим ключом, на мой взгляд, странное -- функция молча возвращает не модифицированную map. Лучше было бы выбросить исключение.

## Обновление значений

Основной синтаксис:

```elixir
new_map = %{ old_map | key1 => value1, key2 => value2 }
```

Альтернативный способ -- функция Map.put/3

### Map.replace

```elixir
replace(map(), key(), value()) :: map()
```

Эта функция дополняет пару **put** и **put_new**. Если **put** и добавляет новый ключ, и обновляет старый, а **put_new** только добавляет новый ключ, то еще нужна функция, которая только обновляет старый ключ. И это функция **replace**.

```elixir-iex
iex(3)> Map.replace(room, :floor, 7)
%{floor: 7, number: 610}
iex(4)> Map.replace(room, :size, :big)
%{floor: 6, number: 610}
```

Как и put_new, эта функция молча возвращает неизмененную map, если нужного ключа в ней нет.

Но еще есть функция **replace!**, которая бросает исключение:

```elixir-iex
iex(5)> Map.replace!(room, :floor, 7)
%{floor: 7, number: 610}
iex(6)> Map.replace!(room, :size, :big)
** (KeyError) key :size not found in: ...
```

## Другие полезные функции модуля Map

keys, values
drop, take

```elixir-iex
iex(1)> map = %{name: "Dave", likes: "Programming", where: "Dallas"}
%{name: "Dave", where: "Dallas", likes: "Programming"}

iex(2)> Map.keys(map)
[:name, :where, :likes]
iex(3)> Map.values(map)
["Dave", "Dallas", "Programming"]

iex(6)> Map.drop(map, [:where, :likes])
%{name: "Dave"}
iex(7)> Map.take(map, [:where, :likes])
%{where: "Dallas", likes: "Programming"}
```


## Упражнение №1

Посчитать, сколько раз встречается каждое слово в строке.

```elixir-iex
iex(1)> c "lib/map_example.exs"
[MapExample, MapExampleTest]
iex(2)> str = MapExample.test_string
"Elixir in Action is a tutorial book that aims to bring developers\nnew to Elixir and Erlang to the point where they can develop complex systems on their own.\n"
iex(3)> MapExample.count_words(str)
%{
  "Action" => 1,
  "Elixir" => 2,
  "Erlang" => 1,
  "a" => 1,
  "aims" => 1,
  "and" => 1,
  "book" => 1,
  "bring" => 1,
  "can" => 1,
  "complex" => 1,
  "develop" => 1,
  "developers" => 1,
  "in" => 1,
  "is" => 1,
  "new" => 1,
  "on" => 1,
  "own." => 1,
  "point" => 1,
  "systems" => 1,
  "that" => 1,
  "the" => 1,
  "their" => 1,
  "they" => 1,
  "to" => 3,
  "tutorial" => 1,
  "where" => 1
}
```

Запуск теста:

```shell
elixir lib/map_example.exs
.
Finished in 0.04 seconds (0.04s on load, 0.00s async, 0.00s sync)
1 test, 0 failures

Randomized with seed 145339
```

## Упражнение №2

Конвертор валют
