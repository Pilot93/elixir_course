# Использование Map

Теперь смоделируем те же сущности с помощью словарей:

```
defmodule MyCalendar.Model.Map do

  defmodule Place do

    def new(office, room) do
      %{
        office: office,
        room: room
      }
    end

  end

  defmodule Participant do

    def new(name, role) do
      %{
        name: name,
        role: role
      }
    end

  end

  defmodule Topic do

    def new(subject, description) do
      %{
        subject: subject,
        description: description
      }
    end

  end

  defmodule Event do

    def new(title, place, time, participants, agenda) do
      %{
        title: title,
        place: place,
        time: time,
        participants: participants,
        agenda: agenda
      }
    end

  end

end
```

Создадим встречу:
```
defmodule MyCalendar do

  ...

  def sample_event_map() do
    alias MyCalendar.Model.Map, as: M

    place = M.Place.new("Office #1", "Room 123")
    time = ~U[2024-07-05 15:00:00Z]
    participants = [
      M.Participant.new("Kate", :project_manager),
      M.Participant.new("Bob", :developer),
      M.Participant.new("Bill", :qa),
    ]
    agenda = [
      M.Topic.new("Release MyCalendar 1.0", "disscuss release"),
      M.Topic.new("Buy cookies", "disscuss cookies")
    ]
    M.Event.new("Team Meeting", place, time, participants, agenda)
  end
end
```

И запустим:
```
$ iex -S mix
iex(1)> event = MyCalendar.sample_event_map
%{
  time: ~U[2024-07-05 15:00:00Z],
  title: "Team Meeting",
  participants: [
    %{name: "Kate", role: :project_manager},
    %{name: "Bob", role: :developer},
    %{name: "Bill", role: :qa}
  ],
  place: %{office: "Office #1", room: "Room 123"},
  agenda: [
    %{description: "disscuss release", subject: "Release MyCalendar 1.0"},
    %{description: "disscuss cookies", subject: "Buy cookies"}
  ]
}
```

Посмотрим на некоторые продвинутые способы работы со словарями.


## Map.update/4

Полезная функция, но с довольно сложными аргументами, с которыми нужно разобраться.

```elixir
update(
  map(),
  key(),
  default :: value(),
  (existing_value :: value() -> updated_value :: value())
) :: map()
```

Проще всего сделать это на конкретном примере.

Допустим, мы хотим добавить в уже готовое событие еще одного участника.

```elixir-iex
iex(2)> alias MyCalendar.Model.Map, as: M
iex(3)> helen = M.Participant.new("Helen", :developer)
%{name: "Helen", role: :developer}
```

Можно пойти сложным путём, по-очереди обновляя вложенные ключи словаря:

```elixir-iex
> participants = [helen | event.participants]
> event = %{event | participants: participants}
```

Или можно вызывать функцию `Map.update/4`:

```elixir-iex
> john = M.Participant.new("John", :ba)
> Map.update(event, :participants, [], fn p -> [john | p] end)
```

Первый аргумент -- словарь, второй -- ключ, значение которого нужно обновить. Четвертый аргумент -- анонимная функция, которая принимает старое значение ключа, и должна вернуть новое значение ключа.

Тут есть еще третий аргумент, который используется как значение по умолчанию, если данного ключа нет в map. Тогда ключ добавляется с этим значением, а анонимная функция не вызывается.

Есть еще функция **update!** с тремя аргументами, которая бросает исключение в случае отсутствия ключа, и значение по умолчанию ей не нужно.

```elixir-iex
> maria = M.Participant.new("Maria", :hr)
> Map.update!(event, :participants, fn p -> [maria | p] end)
```

Мы можем добавить такую функцию в модуль Event:

```
  defmodule Event do

    ...

    def add_participant(event, participant) do
      Map.update!(event, :participants, fn p -> [participant | p] end)
    end

  end
```

```
> recompile
> M.Event.add_participant(event, john)
```

## get_in, put_in, update_in

В 4-м уроке я упомнил набор функций и макросов `get_in`, `put_in`, `update_in` для работы с вложнными словарями. Сейчас познакомимся с ними подробнее.

TODO прочитать доки про эти макросы и модуль Access.
If a key given to get_in/2 is a function -- что полезног можно с этим сделать?


**get_in**

```
> event = MyCalendar.sample_event_map()
> event.place.room
"Room 123"
> get_in(event, [:place, :room])
"Room 123"
```

`get_in` имеет только форму функции, потому что чтение доступно и так, без макроса.

Ситуация немного сложнее, если у нас есть вложенный список. Тогда нам понадобится функция-итератор по этому списку:

```
> participants_iterator = fn :get, participants, next -> Enum.map(participants, next) end
> get_in(event, [:participants, participants_iterator, :name])
> get_in(event, [:participants, participants_iterator, :role])

> agenda_iterator = fn :get, topics, next -> Enum.map(topics, next) end
> get_in(event, [:agenda, agenda_iterator, :subject])
```

На самом деле у нас получились одинаковые итераторы, которые просто возвращают элемент списка. Для такого случая есть готовый итератор:

```
get_in(event, [:agenda, Access.all(), :subject])
```

Или мы можем использовать более специфичный итератор:
```
> agenda_iterator = fn :get, topics, _next -> topics end
> get_in(event, [:agenda, agenda_iterator])
```

**put_in**

```elixir-iex
event = put_in(event.location.room.number, 611)
event = put_in(event, [:location, :room, :number], 612)
```

put_in имеет форму и функции, и макроса. Макрос разворачивается в одноименную функцию. А функция выполняет такое же каскадное обновление, как мы делали вручную.

**update_in**

```elixir-iex
event = update_in(event.location.room.number, fn(number) -> number + 10 end)
event = update_in(event, [:location, :room, :number], fn(number) -> number + 10 end)
```

Макросы подходят, когда весь путь известен статически, на этапе компиляции. Функции подходят, когда путь формируется динамически в рантайме:

```elixir-iex
keys = [:location, :room, :number]
event = update_in(event, keys, fn(number) -> number + 10 end)
```


## split, take, drop

TODO my_calendar sample

Эта группа функций не самая популярная, но полезная в некоторых случаях. Работа с нашим event не такой случай, тут нужен другой пример.

Допустим, у нас есть map с населением крупных городов Европы:

```elixir-iex
iex(1)> population = EuropeCity.population()
%{
  "Barcelona" => 1636762,
  "Belgrade" => 1397939,
  "Berlin" => 3748148,
  ...
```

И список городов, находящихся в странах-членах Евросоюза:

```elixir-iex
iex(2)> eu_cities = EuropeCity.eu_cities()
["Berlin", "Madrid", "Rome", ...
```

С помощью Map.take мы можем выбрать информацию по населению только тех городов, которые входят в Евросоюз:

```elixir-iex
iex(3)> Map.take(population, eu_cities)
```

Функция принимает map и список ключей, отбрасывает из map ключи, не входящие в список, и возвращает новую map. Если в списке есть ключи, которых нет в map, то они просто игнорируются.

Map.drop работает противоположным образом, возвращает map с ключами, не входящими в список.

```elixir-iex
iex(4)> Map.drop(population, eu_cities)
```

То есть мы получили население городов, не входящих в Евросоюз.

Функция Map.split объединяет take и drop. Она разделяет map на две новые map. Первая содержит ключи из списка, вторая не содержит.

```elixir-iex
iex(5)> Map.split(population, eu_cities)
```

## merge

TODO my_calendar sample

Мы разобрались, как разделить map на две. Теперь посмотрим на противоположную операцию -- объединение двух map в одну.

Для примера возьмем любимую музыку, мою:

```elixir-iex
iex(1)> yura_fs = BestSong.yura_favorite_songs()
%{
  "Bel Suono" => "Libertango",
  "Péter Bence" => "Despacito",
  "The Manhattan Transfer" => "The Offbeat of Avenues"
}
```

и кота Тихона:

```elixir-iex
iex(2)> tihon_fs = BestSong.tihon_favorite_songs()
%{
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Africa",
  "The Manhattan Transfer" => "The Junction"
}
```

Как видно, наши вкусы частично пересекаются. Мы имеем две map по три ключа в каждой, причем один ключ уникальный, а два конфликтуют.

Посмотрим, что будет, если применить функцию `Map.merge`:

```elixir-iex
iex(3)> Map.merge(yura_fs, tihon_fs)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Africa",
  "The Manhattan Transfer" => "The Junction"
}
iex(4)> Map.merge(tihon_fs, yura_fs)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Despacito",
  "The Manhattan Transfer" => "The Offbeat of Avenues"
}
```

Функция объединяет две map в одну. И если есть конфликтующие ключи, то значения для них берутся из map, которая передана вторым аргументом.

Это не всегда подходящая стратегия разрешения конфликтов. Если у нас есть стратегия лучше, то мы можем воспользоваться `Map.merge/3`, где третим аргументом передается функция, разрешающая конфликт:

```elixir-iex
Map.merge(yura_fs, tihon_fs, fn (_artist, song_1, song_2) -> [song_1, song_2] end)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => ["Despacito", "Africa"],
  "The Manhattan Transfer" => ["The Offbeat of Avenues", "The Junction"]
}
```

Как видно, функция принимает ключ и оба конфликтующих значения и должна вернуть новое значение.

## map, reduce, filter

TODO my_calendar sample

Напоследок вспомним, что все функции модуля Enum, которые мы изучали в 5-м уроке, работают с map.

```elixir-iex
iex(10)> Enum.map(yura_fs, fn ({artist, _song}) -> artist end)
["Bel Suono", "Péter Bence", "The Manhattan Transfer"]

iex(14)> Enum.filter(population, fn({_, p}) -> p > 5_000_000 end)
[
  {"Istanbul", 15462452},
  {"London", 9126366},
  {"Moscow", 12195221},
  {"Saint Petersburg", 5383890}
]

iex(17)> Enum.reduce(population, 0, fn({city, p}, acc) ->
...(17)> if city in eu_cities, do: acc + p, else: acc
...(17)> end)
25684115
```

Это работает потому, что модуль Enum поддерживает любые структуры данных, реализующие протокол Enumerable. Наша следущая тема -- **протокол**. Разберемся, что это такое.
